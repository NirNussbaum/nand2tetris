class SpongbobGame{

   field Obstacle ob; 
   field Spongbob spongbob;
   field int direction; // the square's current direction: 
                         // 0=none, 1=up, 2=down, 3=left, 4=right
   field int score;
    /** Constructs a new Spongbob Game. */
   constructor SpongbobGame new() {
       // Creates a line and a Spongbob and positions it on the line.
       var Array obHeights;
       let obHeights = Array.new(5);
       let obHeights[0] = 10;
       let obHeights[1] = 15;
       let obHeights[2] = 20;
       let obHeights[3] = 25; 
       let obHeights[4] = 30; 
       let ob = Obstacle.new(obHeights, 5, 245);
       let spongbob = Spongbob.new(30,229); //Starting pos of Spongbob.
       let direction = 0;  // initial state is no movement
       do Screen.drawLine(0, 245, 500, 245);
       let score = 0;
       return this;
   }
 
    /** Disposes this game. */
   method void dispose() {
       do spongbob.dispose();
       do ob.dispose();
       do Memory.deAlloc(this);
       return;
   }
 
    /** Moves the Spongbob and the obstacle. */
   method void moveSpongbob() {
       do spongbob.erase();
       do spongbob.draw();
       do ob.erase();
       if(ob.draw()){
         let score = score + 1;
       }
       do Sys.wait(25);  // delays the next movement
       return;
   }
        
   /** check if the Spongbob collusion with an obstacle. */
   method boolean collusion(){
      if(16 > ob.get_x()){
         if(spongbob.get_y() > ob.get_y()){
            return true;
         }
      }
      return false;
   }
 
    /** Runs the game: handles the user's inputs and moves the Spongbob accordingly */
   method void run() {
       var char key;  // the key currently pressed by the user.
       var boolean exit;
       let exit = false;
       while (~exit) {
         do Output.moveCursor(0,25);
         do Output.printString("Spongbob Score: ");
         do Output.printInt(score);
         let key = Keyboard.keyPressed();
         do moveSpongbob();
         if (key = 81)  { let exit = true; }    // q key
         if (key = 140)  { let exit = true; } // esc key
         if (key = 32) { do spongbob.jump(); }   // space key
         if (collusion()){
            let exit = true;
         }
      }
    return;
   }
}
 
 
 